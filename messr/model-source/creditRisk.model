%% Module: Credit risk 


%% Aggregate variables 

!variables

    "Average portfolio default rate" q


!shocks

    "Shock to portfolio default rate" shock_q


!equations

    "Portfolio default rate (probability of default)"
    q = (!for <1 : K> !do + q_? * lp_? !end) / lp; 


!log-variables !all-but
    q


%% Loan segments 

!for ? = <1 : K> !do

    !variables

        "Portfolio default rate, ?" q_?
        "Composite Macro Indicator, ?" z_?


    !parameters 

        ss_q_?, c2_q_?, c3_q_?, c4_q_?, c5_q_?
        c1_z_?


    !shocks

        "Shock to portfolio default rate, ?" shock_q_?


    !equations

        "Composite macro and borrower leverage indicator"
        z_? = ...
            + log(y_gap) ...
            - c1_z_? * [ l_?/(4*py*fws_y) - l_to_4ny_tnd_?];


        "Portfolio default rate (probability of default)"
        q_? = ...
            + glogc1(-z_?, ss_q_?, c2_q_?, c3_q_?, c4_q_?, c5_q_?) ...
            + shock_q_? + shock_q ...
        !! q_? = ss_q_?;


    !log-variables !all-but

        q_?, z_?

!end

